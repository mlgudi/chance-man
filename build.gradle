plugins {
	id 'java'
}

repositories {
	mavenLocal()
	maven {
		url = 'https://repo.runelite.net'
		content {
			includeGroupByRegex("net\\.runelite.*")
		}
	}
	mavenCentral()
	gradlePluginPortal()
}

def runeLiteVersion = 'latest.release'

configurations {
	core
	thirdParty.extendsFrom core
}

dependencies {
	compileOnly group: 'net.runelite', name:'client', version: runeLiteVersion

	// Chance Man (DropFetcher)
	thirdParty("org.jsoup:jsoup:1.13.1") {
		because "chance-man: HTML parsing for NPC drop tables"
	}

	implementation 'org.jsoup:jsoup:1.13.1'

	compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'

	testImplementation 'junit:junit:4.13.2'
	testImplementation group: 'net.runelite', name:'client', version: runeLiteVersion
	testImplementation group: 'net.runelite', name:'jshell', version: runeLiteVersion
}

group = 'com.chanceman'
version = '2.2.0'

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
	options.release.set(11)
}

tasks.register('shadowJar', Jar) {
	dependsOn configurations.testRuntimeClasspath
	manifest {
		attributes('Main-Class': 'com.example.ExamplePluginTest', 'Multi-Release': true)
	}

	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	from sourceSets.main.output
	from sourceSets.test.output
	from {
		configurations.testRuntimeClasspath.collect { file ->
			file.isDirectory() ? file : zipTree(file)
		}
	}

	exclude 'META-INF/INDEX.LIST'
	exclude 'META-INF/*.SF'
	exclude 'META-INF/*.DSA'
	exclude 'META-INF/*.RSA'
	exclude '**/module-info.class'

	group = BasePlugin.BUILD_GROUP
	archiveClassifier.set('shadow')
	archiveFileName.set("${rootProject.name}-${project.version}-all.jar")
}

task verifyCore {
	doLast {
		configurations.core.asPath
	}
}

task verifyAll {
	doLast {
		configurations.thirdParty.asPath

		if (file("build.gradle").text =~ /(?m)^ /) {
			throw new RuntimeException("gradle files should be indented with tabs")
		}
	}
}